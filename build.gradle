plugins {
	id 'java'
	id 'org.springframework.boot' version '3.4.4'
	id 'io.spring.dependency-management' version '1.1.7'
}

group = 'com.dodream'
version = '0.0.1-SNAPSHOT'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

apply from:"gradle/spring.gradle"
apply from:"gradle/swagger.gradle"
apply from:"gradle/db.gradle"
apply from:"gradle/test.gradle"
apply from:"gradle/jwt.gradle"
apply from:"gradle/jackson.gradle"

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:2024.0.0"
    }
}

tasks.named('test') {
	useJUnitPlatform()
}

def isCI = System.getenv("CI") == "true"

if (isCI) {
    tasks.register('copyAppYml', Copy) {
        description = 'Copy application*.yml from submodule to resources (CI only)'
        from('src/main/resources/secret') {
            include 'application.yml', 'application-main.yml'
        }
        into('src/main/resources')
    }


    tasks.register('copyDockerCompose', Copy) {
        description = 'Copy docker-compose file from submodule (CI only)'
        from('src/main/resources/secret') {
            include 'docker-compose-main.yml'
        }
        into('.')
    }

    tasks.named('copyAppYml') {
           mustRunAfter 'copyDockerCompose'
    }

    processResources.dependsOn('copyAppYml', 'copyDockerCompose')
    bootRun.dependsOn('copyAppYml', 'copyDockerCompose')

    tasks.named('compileJava') {
            dependsOn 'copyAppYml', 'copyDockerCompose'
    }
}
